#
# qmake configuration for building with arm-angstrom-linux-gnueabi-g++
#

include(../../common/linux.conf)
include(../../common/gcc-base-unix.conf)
include(../../common/g++-unix.conf)
include(../../common/qws.conf)

# modifications to g++.conf
# check if we have a qmake built-in prefix first
QT_TOOLCHAIN_PREFIX     = $$[QT_TOOLCHAIN_PREFIX]
isEmpty(QT_TOOLCHAIN_PREFIX) {
    QT_TOOLCHAIN_PREFIX = $(OE_TOOLCHAIN_PREFIX)
}
QMAKE_CC            = $${QT_TOOLCHAIN_PREFIX}gcc
QMAKE_CXX           = $${QT_TOOLCHAIN_PREFIX}g++
#workaround for qt's configure not handling variable expansion
isEmpty(QT_TOOLCHAIN_PREFIX) {
    QMAKE_CC            = arm-angstrom-linux-gnueabi-gcc
    QMAKE_CXX           = arm-angstrom-linux-gnueabi-g++
}

QMAKE_LINK              = $${QMAKE_CXX}
QMAKE_LINK_SHLIB        = $${QMAKE_CXX}

# modifications to linux.conf
QMAKE_AR                = $${QT_TOOLCHAIN_PREFIX}ar cqs
QMAKE_OBJCOPY           = $${QT_TOOLCHAIN_PREFIX}objcopy
QMAKE_STRIP             = $${QT_TOOLCHAIN_PREFIX}strip

QT_TOOLCHAIN_CXXFLAGS               = $$[QT_TOOLCHAIN_CXXFLAGS]
!isEmpty(QT_TOOLCHAIN_CXXFLAGS) {
    # we need that crazy sed stuff because qmake doesn't let us store '-' and '='
    QMAKE_CFLAGS       += $$system(echo $$[QT_TOOLCHAIN_CFLAGS] | sed -e  \"s/:/=/g\" -e \"s/ *\\([^ ]*\\)/ -\\1/g\")
    QMAKE_CXXFLAGS       += $$system(echo $$[QT_TOOLCHAIN_CXXFLAGS] | sed -e  \"s/:/=/g\" -e \"s/ *\\([^ ]*\\)/ -\\1/g\")
    QMAKE_LFLAGS       += $$system(echo $$[QT_TOOLCHAIN_LFLAGS] | sed -e  \"s/:/=/g\" -e \"s/ *\\([^ ]*\\)/ -\\1/g\")
} else {
	QT_TOOLCHAIN_CXXFLAGS         = $(OE_TOOLCHAIN_ARGS) $(TARGET_CXXFLAGS)
    QMAKE_CFLAGS       += $(OE_TOOLCHAIN_ARGS) $(TARGET_CFLAGS)
    QMAKE_CXXFLAGS     += $(OE_TOOLCHAIN_ARGS) $(TARGET_CXXFLAGS)
    QMAKE_LFLAGS       += $(TARGET_LDFLAGS)
}
#workaround for qt's configure not handling variable expansion
isEmpty(QT_TOOLCHAIN_CXXFLAGS) {
    QMAKE_CFLAGS       += -march=armv7-a -mtune=cortex-a8 -mfpu=neon -mfloat-abi=softfp
    QMAKE_CXXFLAGS     += -march=armv7-a -mtune=cortex-a8 -mfpu=neon -mfloat-abi=softfp
    QMAKE_LFLAGS       += 
}

#modifications to gcc-base.conf
QMAKE_CXXFLAGS_RELEASE += -O3
QMAKE_LIBS             += -lrt -lpthread -ldl

load(qt_config)

# Extra stuff (DirectFB, OpenGL...)
QMAKE_INCDIR_EGL     = $${QT_SYSROOT}/usr/include
QMAKE_LIBDIR_EGL     = $${QT_SYSROOT}/usr/lib
QMAKE_INCDIR_OPENGL_ES2 = $${QMAKE_INCDIR_EGL}
QMAKE_LIBDIR_OPENGL_ES2 = $${QMAKE_LIBDIR_EGL}

QMAKE_LIBS_EGL        = -lEGL -lIMGegl -lsrv_um
QMAKE_LIBS_OPENGL_ES2 = -lGLESv2 $${QMAKE_LIBS_EGL}

